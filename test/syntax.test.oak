std := import('std')
{
	tokenize: tokenize
	parse: parse
	print: print
} := import('syntax')

fn run(t) {
	// tokenizer tests
	{
		fn Token(type, pos, val) {
			type: type
			val: val
			pos: pos
		}

		'empty program' |> t.eq(
			tokenize('')
			[]
		)

		'comment program' |> t.eq(
			tokenize('// this is a comment\n//second comment')
			[
				Token(:comment, [1, 1], ' this is a comment')
				Token(:newline, [1, 20])
				Token(:comment, [2, 1], 'second comment')
			]
		)

		'whitespace-only program' |> t.eq(
			tokenize('   \n')
			[
				Token(:newline, [1, 3])
			]
		)

		'null and empty' |> t.eq(
			tokenize('?, _, __')
			[
				Token(:qmark, [1, 1])
				Token(:comma, [1, 2])
				Token(:underscore, [1, 4])
				Token(:comma, [1, 5])
				Token(:identifier, [1, 7], '__')
				Token(:comma, [1, 8])
			]
		)

		'number literals' |> t.eq(
			tokenize(' 0 1 2.34 -0.567')
			[
				Token(:numberLiteral, [1, 2], '0')
				Token(:numberLiteral, [1, 4], '1')
				Token(:numberLiteral, [1, 6], '2.34')
				Token(:minus, [1, 11])
				Token(:numberLiteral, [1, 12], '0.567')
				Token(:comma, [1, 16])
			]
		)

		'string literals' |> t.eq(
			tokenize('\'hello\' \'hi\' \'what\\\'s up\\n\\t\' ')
			[
				Token(:stringLiteral, [1, 1], 'hello')
				Token(:stringLiteral, [1, 9], 'hi')
				Token(:stringLiteral, [1, 14], 'what\\\'s up\\n\\t')
				Token(:comma, [1, 30])
			]
		)

		'atom literals' |> t.eq(
			tokenize(':whatever :not_found_404')
			[
				Token(:colon, [1, 1])
				Token(:identifier, [1, 2], 'whatever')
				Token(:colon, [1, 11])
				Token(:identifier, [1, 12], 'not_found_404')
				Token(:comma, [1, 24])
			]
		)

		'identifiers' |> t.eq(
			tokenize('hi _hello? whats_up__ nothing! ')
			[
				Token(:identifier, [1, 1], 'hi')
				Token(:identifier, [1, 4], '_hello?')
				Token(:identifier, [1, 12], 'whats_up__')
				Token(:identifier, [1, 23], 'nothing!')
				Token(:comma, [1, 31])
			]
		)

		'simple binary expression' |> t.eq(
			tokenize('total := 1 + 2 * 4')
			[
				Token(:identifier, [1, 1], 'total')
				Token(:assign, [1, 7])
				Token(:numberLiteral, [1, 10], '1')
				Token(:plus, [1, 12])
				Token(:numberLiteral, [1, 14], '2')
				Token(:times, [1, 16])
				Token(:numberLiteral, [1, 18], '4')
				Token(:comma, [1, 18])
			]
		)
		'hanging binary expression' |> t.eq(
			tokenize('1 + 2 +\n3 *\n4')
			[
				Token(:numberLiteral, [1, 1], '1')
				Token(:plus, [1, 3])
				Token(:numberLiteral, [1, 5], '2')
				Token(:plus, [1, 7])
				Token(:newline, [1, 7])
				Token(:numberLiteral, [2, 1], '3')
				Token(:times, [2, 3])
				Token(:newline, [2, 3])
				Token(:numberLiteral, [3, 1], '4')
				Token(:comma, [3, 1])
			]
		)

		'delimiters' |> t.eq(
			tokenize('( [{ hi: :hello }] ) + (2)')
			[
				Token(:leftParen, [1, 1])
				Token(:leftBracket, [1, 3])
				Token(:leftBrace, [1, 4])
				Token(:identifier, [1, 6], 'hi')
				Token(:colon, [1, 8])
				Token(:colon, [1, 10])
				Token(:identifier, [1, 11], 'hello')
				Token(:comma, [1, 17])
				Token(:rightBrace, [1, 17])
				Token(:comma, [1, 18])
				Token(:rightBracket, [1, 18])
				Token(:comma, [1, 20])
				Token(:rightParen, [1, 20])
				Token(:plus, [1, 22])
				Token(:leftParen, [1, 24])
				Token(:numberLiteral, [1, 25], '2')
				Token(:comma, [1, 26])
				Token(:rightParen, [1, 26])
				Token(:comma, [1, 26])
			]
		)

		'list literal' |> t.eq(
			tokenize('[1, a, :first]')
			[
				Token(:leftBracket, [1, 1])
				Token(:numberLiteral, [1, 2], '1')
				Token(:comma, [1, 3])
				Token(:identifier, [1, 5], 'a')
				Token(:comma, [1, 6])
				Token(:colon, [1, 8])
				Token(:identifier, [1, 9], 'first')
				Token(:comma, [1, 14])
				Token(:rightBracket, [1, 14])
				Token(:comma, [1, 14])
			]
		)

		'list literal with newlines' |> t.eq(
			tokenize('[\n\t1,\n\ta,\n\t:first\n]')
			[
				Token(:leftBracket, [1, 1])
				Token(:newline, [1, 1])
				Token(:numberLiteral, [2, 2], '1')
				Token(:comma, [2, 3])
				Token(:newline, [2, 3])
				Token(:identifier, [3, 2], 'a')
				Token(:comma, [3, 3])
				Token(:newline, [3, 3])
				Token(:colon, [4, 2])
				Token(:identifier, [4, 3], 'first')
				Token(:comma, [4, 0])
				Token(:newline, [4, 0])
				Token(:rightBracket, [5, 1])
				Token(:comma, [5, 1])
			]
		)

		'empty object' |> t.eq(
			tokenize('{}')
			[
				Token(:leftBrace, [1, 1])
				Token(:rightBrace, [1, 2])
				Token(:comma, [1, 2])
			]
		)

		'object literal' |> t.eq(
			tokenize('{a: :bee}')
			[
				Token(:leftBrace, [1, 1])
				Token(:identifier, [1, 2], 'a')
				Token(:colon, [1, 3])
				Token(:colon, [1, 5])
				Token(:identifier, [1, 6], 'bee')
				Token(:comma, [1, 9])
				Token(:rightBrace, [1, 9])
				Token(:comma, [1, 9])
			]
		)

		'object literal with newlines' |> t.eq(
			tokenize('{\n1: 2\n3: ?\n}')
			[
				Token(:leftBrace, [1, 1])
				Token(:newline, [1, 1])
				Token(:numberLiteral, [2, 1], '1')
				Token(:colon, [2, 2])
				Token(:numberLiteral, [2, 4], '2')
				Token(:comma, [2, 0])
				Token(:newline, [2, 0])
				Token(:numberLiteral, [3, 1], '3')
				Token(:colon, [3, 2])
				Token(:qmark, [3, 4])
				Token(:comma, [3, 4])
				Token(:newline, [3, 4])
				Token(:rightBrace, [4, 1])
				Token(:comma, [4, 1])
			]
		)

		'basic function' |> t.eq(
			tokenize('fn a(b, c) if a |> c {\nd -> e\n}')
			[
				Token(:fnKeyword, [1, 1])
				Token(:identifier, [1, 4], 'a')
				Token(:leftParen, [1, 5])
				Token(:identifier, [1, 6], 'b')
				Token(:comma, [1, 7])
				Token(:identifier, [1, 9], 'c')
				Token(:comma, [1, 10])
				Token(:rightParen, [1, 10])
				Token(:ifKeyword, [1, 12])
				Token(:identifier, [1, 15], 'a')
				Token(:pipeArrow, [1, 17])
				Token(:identifier, [1, 20], 'c')
				Token(:leftBrace, [1, 22])
				Token(:newline, [1, 22])
				Token(:identifier, [2, 1], 'd')
				Token(:branchArrow, [2, 3])
				Token(:identifier, [2, 6], 'e')
				Token(:comma, [2, 0])
				Token(:newline, [2, 0])
				Token(:rightBrace, [3, 1])
				Token(:comma, [3, 1])
			]
		)

		'variadic function' |> t.eq(
			tokenize('fn f(x, y...) ?, f(1, a...)')
			[
				Token(:fnKeyword, [1, 1])
				Token(:identifier, [1, 4], 'f')
				Token(:leftParen, [1, 5])
				Token(:identifier, [1, 6], 'x')
				Token(:comma, [1, 7])
				Token(:identifier, [1, 9], 'y')
				Token(:ellipsis, [1, 10])
				Token(:comma, [1, 13])
				Token(:rightParen, [1, 13])
				Token(:qmark, [1, 15])
				Token(:comma, [1, 16])
				Token(:identifier, [1, 18], 'f')
				Token(:leftParen, [1, 19])
				Token(:numberLiteral, [1, 20], '1')
				Token(:comma, [1, 21])
				Token(:identifier, [1, 23], 'a')
				Token(:ellipsis, [1, 24])
				Token(:comma, [1, 27])
				Token(:rightParen, [1, 27])
				Token(:comma, [1, 27])
			]
		)

		'with expression' |> t.eq(
			tokenize('with readFile(path) fn(file) {\n\t// do thing\n}')
			[
				Token(:withKeyword, [1, 1])
				Token(:identifier, [1, 6], 'readFile')
				Token(:leftParen, [1, 14])
				Token(:identifier, [1, 15], 'path')
				Token(:comma, [1, 19])
				Token(:rightParen, [1, 19])
				Token(:fnKeyword, [1, 21])
				Token(:leftParen, [1, 23])
				Token(:identifier, [1, 24], 'file')
				Token(:comma, [1, 28])
				Token(:rightParen, [1, 28])
				Token(:leftBrace, [1, 30])
				Token(:newline, [1, 30])
				Token(:comment, [2, 2], ' do thing')
				Token(:newline, [2, 12])
				Token(:rightBrace, [3, 1])
				Token(:comma, [3, 1])
			]
		)
	}

	// parser tests
	{
		// for positions
		fn at(line, col) {
			type: _
			val: _
			pos: [line, col]
		}

		'empty program' |> t.eq(
			parse('')
			[]
		)

		'comment program' |> t.eq(
			parse('// this is a comment\n//second comment')
			[]
		)

		'whitespace-only program' |> t.eq(
			parse('   \n')
			[]
		)

		'boolean literals' |> t.eq(
			parse('true, false')
			[
				{ type: :boolean, val: true, tok: at(1, 1) }
				{ type: :boolean, val: false, tok: at(1, 7) }
			]
		)

		'number literals' |> t.eq(
			parse('12, 24.5, 0')
			[
				{ type: :number, int?: true, val: 12, tok: at(1, 1) }
				{ type: :number, int?: false, val: 24.5, tok: at(1, 5) }
				{ type: :number, int?: true, val: 0, tok: at(1, 11) }
			]
		)

		'string literals' |> t.eq(
			parse('\'hello\', \'hi\', \'what\\\'s up\\n\\t\' ')
			[
				{ type: :string, val: 'hello', tok: at(1, 1) }
				{ type: :string, val: 'hi', tok: at(1, 10) }
				{ type: :string, val: 'what\'s up\n\t', tok: at(1, 16) }
			]
		)

		'identifiers' |> t.eq(
			parse('hi, _hello?, whats_up__, nothing! ')
			[
				{ type: :identifier, val: 'hi', tok: at(1, 1) }
				{ type: :identifier, val: '_hello?', tok: at(1, 5) }
				{ type: :identifier, val: 'whats_up__', tok: at(1, 14) }
				{ type: :identifier, val: 'nothing!', tok: at(1, 26) }
			]
		)

		'simple binary expressions' |> t.eq(
			parse('10 + 20')
			[{
				type: :binary
				op: :plus
				left: { type: :number, val: 10, int?: true, tok: at(1, 1) }
				right: { type: :number, val: 20, int?: true, tok: at(1, 6) }
				tok: at(1, 4)
			}]
		)

		'simple assignment' |> t.eq(
			parse('x <- :hi')
			[{
				type: :assignment
				tok: at(1, 3)
				local?: false
				left: { type: :identifier, val: 'x', tok: at(1, 1) }
				right: { type: :atom, val: 'hi', tok: at(1, 6) }
			}]
		)

		'compound binary expressions' |> t.eq(
			parse('total := 1 + 2 * 3 - 4')
			[{
				type: :assignment
				tok: at(1, 7)
				local?: true
				left: { type: :identifier, val: 'total', tok: at(1, 1) }
				right: {
					type: :binary
					tok: at(1, 20)
					op: :minus
					left: {
						type: :binary
						tok: at(1, 12)
						op: :plus
						left: {
							type: :number
							tok: at(1, 10)
							int?: true
							val: 1
						}
						right: {
							type: :binary
							tok: at(1, 16)
							op: :times
							left: {
								type: :number
								tok: at(1, 14)
								int?: true
								val: 2
							}
							right: {
								type: :number
								tok: at(1, 18)
								int?: true
								val: 3
							}
						}
					}
					right: {
						type: :number
						tok: at(1, 22)
						int?: true
						val: 4
					}
				}
			}]
		)

		'list literals' |> t.eq(
			parse('[], [1, [2, 3]]')
			[{
				type: :list
				tok: at(1, 1)
				elems: []
			}, {
				type: :list
				tok: at(1, 5)
				elems: [{
					type: :number
					tok: at(1, 6)
					int?: true
					val: 1
				}, {
					type: :list
					tok: at(1, 9)
					elems: [{
						type: :number
						tok: at(1, 10)
						int?: true
						val: 2
					}, {
						type: :number
						tok: at(1, 13)
						int?: true
						val: 3
					}]
				}]
			}]
		)

		'object literals' |> t.eq(
			parse('{}, {a: :ay, b + [c]: {d: \'dee\'}}')
			[{
				type: :object
				tok: at(1, 1)
				entries: []
			}, {
				type: :object
				tok: at(1, 5)
				entries: [{
					key: { type: :identifier, tok: at(1, 6), val: 'a' }
					val: { type: :atom, tok: at(1, 9), val: 'ay' }
				}, {
					key: {
						type: :binary
						tok: at(1, 16)
						op: :plus
						left: { type: :identifier, tok: at(1, 14), val: 'b' }
						right: {
							type: :list
							tok: at(1, 18)
							elems: [{
								type: :identifier
								tok: at(1, 19)
								val: 'c'
							}]
						}
					}
					val: {
						type: :object
						tok: at(1, 23)
						entries: [{
							key: { type: :identifier, tok: at(1, 24), val: 'd' }
							val: { type: :string, tok: at(1, 27), val: 'dee' }
						}]
					}
				}]
			}]
		)

		'empty fn body edge case' |> t.eq(
			parse('fn {}\nfn named(){  }')
			[{
				type: :function
				tok: at(1, 1)
				name: ''
				args: []
				restArg: ''
				body: { type: :block, tok: at(1, 4), exprs: [] }
			}, {
				type: :function
				tok: at(2, 1)
				name: 'named'
				args: []
				restArg: ''
				body: { type: :block, tok: at(2, 11), exprs: [] }
			}]
		)

		'fn def and call' |> t.eq(
			parse('fn get3 { x := 4, 3 }\nget3(1, 2, []...)')
			[{
				type: :function
				tok: at(1, 1)
				name: 'get3'
				args: []
				restArg: ''
				body: {
					type: :block
					tok: at(1, 9)
					exprs: [{
						type: :assignment
						tok: at(1, 13)
						local?: true
						left: { type: :identifier, tok: at(1, 11), val: 'x' }
						right: { type: :number, tok: at(1, 16), int?: true, val: 4 }
					}, {
						type: :number
						tok: at(1, 19)
						int?: true
						val: 3
					}]
				}
			}, {
				type: :fnCall
				tok: at(2, 5)
				function: {
					type: :identifier
					tok: at(2, 1)
					val: 'get3'
				}
				args: [{
					type: :number
					tok: at(2, 6)
					int?: true
					val: 1
				}, {
					type: :number
					tok: at(2, 9)
					int?: true
					val: 2
				}]
				restArg: {
					type: :list
					tok: at(2, 12)
					elems: []
				}
			}]
		)

		'if expression' |> t.eq(
			parse('if 1 + 2 {\n\t\t\t\t3 + 4 -> 5\n\t\t\t\tf(), g() -> 10\n\t\t\t\t_ -> ?\n\t\t\t}')
			[{
				type: :ifExpr
				tok: at(1, 1)
				cond: {
					type: :binary
					tok: at(1, 6)
					op: :plus
					left: {
						type: :number
						tok: at(1, 4)
						int?: true
						val: 1
					}
					right: {
						type: :number
						tok: at(1, 8)
						int?: true
						val: 2
					}
				}
				branches: [
					{
						type: :ifBranch
						target: {
							type: :binary
							tok: at(2, 7)
							op: :plus
							left: {
								type: :number
								tok: at(2, 5)
								int?: true
								val: 3
							}
							right: {
								type: :number
								tok: at(2, 9)
								int?: true
								val: 4
							}
						}
						body: {
							type: :number
							tok: at(2, 14)
							int?: true
							val: 5
						}
					}
					{
						type: :ifBranch
						target: {
							type: :fnCall
							tok: at(3, 6)
							function: {
								type: :identifier
								tok: at(3, 5)
								val: 'f'
							}
							args: []
							restArg: ?
						}
						body: {
							type: :number
							tok: at(3, 17)
							int?: true
							val: 10
						}
					}
					{
						type: :ifBranch
						target: {
							type: :fnCall
							tok: at(3, 11)
							function: {
								type: :identifier
								tok: at(3, 10)
								val: 'g'
							}
							args: []
							restArg: ?
						}
						body: {
							type: :number
							tok: at(3, 17)
							int?: true
							val: 10
						}
					}
					{
						type: :ifBranch
						target: {
							type: :empty
							tok: at(4, 5)
						}
						body: {
							type: :null
							tok: at(4, 10)
						}
					}
				]
			}]
		)

		'if expression with implicit cond' |> t.eq(
			parse('if {\n\t\t\t\t? -> 3\n\t\t\t}')
			[{
				type: :ifExpr
				tok: at(1, 1)
				cond: {
					type: :boolean
					tok: at(1, 1)
					val: true
				}
				branches: [{
					type: :ifBranch
					target: {
						type: :null
						tok: at(2, 5)
					}
					body: {
						type: :number
						tok: at(2, 10)
						int?: true
						val: 3
					}
				}]
			}]
		)

		'empty identifier in fn def' |> t.eq(
			parse('fn(_, i) {}')
			[{
				type: :function
				tok: at(1, 1)
				name: ''
				args: ['_', 'i']
				restArg: ''
				body: { type: :block, tok: at(1, 10), exprs: [] }
			}]
		)

		'rest args in fn def' |> t.eq(
			parse('fn(x, y...) {}')
			[{
				type: :function
				tok: at(1, 1)
				name: ''
				args: ['x']
				restArg: 'y'
				body: { type: :block, tok: at(1, 13), exprs: [] }
			}]
		)

		'with expression' |> t.eq(
			parse('with f(1, 2) 100')
			[{
				type: :fnCall
				tok: at(1, 7)
				function: { type: :identifier, tok: at(1, 6), val: 'f' }
				args: [
					{ type: :number, tok: at(1, 8), int?: true, val: 1 }
					{ type: :number, tok: at(1, 11), int?: true, val: 2 }
					{ type: :number, tok: at(1, 14), int?: true, val: 100 }
				]
				restArg: ?
			}]
		)

		'pipe arrow operator' |> t.eq(
			parse('3 |> double() |> with add() 10')
			[{
				type: :fnCall
				tok: at(1, 26)
				function: {
					type: :identifier
					tok: at(1, 23)
					val: 'add'
				}
				args: [
					{
						type: :fnCall
						tok: at(1, 12)
						function: {
							type: :identifier
							tok: at(1, 6)
							val: 'double'
						}
						args: [{
							type: :number
							tok: at(1, 1)
							int?: true
							val: 3
						}]
						restArg: ?
					}
					{ type: :number, tok: at(1, 29), int?: true, val: 10 }
				]
				restArg: ?
			}]
		)
	}

	// printer integration tests
	{
		'empty program' |> t.eq(
			print('')
			''
		)
		'comment program' |> t.eq(
			print('// this is a comment  \n// \n//    second comment')
			'// this is a comment\n//\n//    second comment'
		)
		'whitespace-only program preserves newlines' |> t.eq(
			print('   \n\t\n')
			'\n\n'
		)
		'number literals' |> t.eq(
			print(' 0, 1\n2.34, -0.5670,')
			'0, 1\n2.34, -0.5670'
		)
		'string literals' |> t.eq(
			print('\'hello\nworld\',\'hi\',\n\'what\\\'s up\\n\\t\' ')
			'\'hello\nworld\', \'hi\'\n\'what\\\'s up\\n\\t\''
		)
		'identifier' |> t.eq(
			print('hi, _hello?, whats_up__, nothing! ')
			'hi, _hello?, whats_up__, nothing!'
		)
		'atoms' |> t.eq(
			print(':whatever, :not_found_404')
			':whatever, :not_found_404'
		)
		'atoms in blocks' |> t.eq(
			print('(:hi), {:hi}')
			'(:hi), { :hi }'
		)
		'simple unary expression' |> t.eq(
			print('!!a+--b')
			'!!a + --b'
		)
		'simple binary expression' |> t.eq(
			print('total:=one ( )+2 *  \t4   ')
			'total := one() + 2 * 4'
		)
		'- (:minus) used as infix op' |> t.eq(
			print('( 1-2 )-3+-2')
			'(1 - 2) - 3 + -2'
		)
		'delimiters' |> t.eq(
			print('( [{hi: :hello}      ] )+(    2 )')
			'([{ hi: :hello }]) + (2)'
		)
		'list literal' |> t.eq(
			print('[   1,a,       :first]')
			'[1, a, :first]'
		)
		'list literal with newlines' |> t.eq(
			print('[\n\t1,\n\ta,\n\t:first\n]')
			'[\n\t1\n\ta\n\t:first\n]'
		)
		'empty object' |> t.eq(
			print('{}')
			'{}'
		)
		'object literal' |> t.eq(
			print(' {   1:2,three: \'four\' , five:[ six\t],}')
			'{ 1: 2, three: \'four\', five: [six] }'
		)
		'object literal with newlines' |> t.eq(
			print('{   \n1:2,\n  three:\'four\'\nfive:six,\nseven::eight\n}')
			'{\n\t1: 2\n\tthree: \'four\'\n\tfive: six\n\tseven: :eight\n}'
		)
		'complex object literal' |> t.eq(
			print('{\nlist: [\n1,2\nthree,\n],\nobj: {__key: val? + 3}\n   }')
			'{\n\tlist: [\n\t\t1, 2\n\t\tthree\n\t]\n\tobj: { __key: val? + 3 }\n}'
		)
		'overlapping braces on object literals' |> t.eq(
			print('{\na: b\n}, {\nc: d\n}')
			'{\n\ta: b\n}, {\n\tc: d\n}'
		)

		'basic function' |> t.eq(
			print('fn a(b, c) if a |> c {\nd -> e(_,{f: F}   ,:g,[ h],(i+ j))\n}')
			'fn a(b, c) if a |> c {\n\td -> e(_, { f: F }, :g, [h], (i + j))\n}'
		)
		'variadic function' |> t.eq(
			print('fn f (  x,y ...\t){?}  ,\n\t\tf(1,[ a ] ...)')
			'fn f(x, y...) { ? }\nf(1, [a]...)'
		)
		'with expression' |> t.eq(
			print('with readFile (path)fn(file){\n\t// do thing\n}')
			'with readFile(path) fn(file) {\n\t// do thing\n}'
		)

		// hanging indents
		'simple hanging indent' |> t.eq(
			print('1 + \n2')
			'1 +\n\t2'
		)
		'simple hanging indent with ending newline' |> t.eq(
			print('1 + \n2\n')
			'1 +\n\t2\n'
		)
		'hanging indent inside delimited list' |> t.eq(
			print('[1, 2, 3\n4, 5, 6]')
			'[1, 2, 3\n\t4, 5, 6]'
		)
		'hanging indent inside delimited list inside parens' |> t.eq(
			print('(\n[1, 2, 3\n4, 5, 6]\n)')
			'(\n\t[1, 2, 3\n\t\t4, 5, 6]\n)'
		)
		'hanging indent inside binary expr' |> t.eq(
			print('total := a + \nb+c')
			'total := a +\n\tb + c'
		)
		'multiline hanging indent in delimited list' |> t.eq(
			print('[ 1,2,3\n4, 5,6\n7, 8, 9 \n10 ]')
			'[1, 2, 3\n\t4, 5, 6\n\t7, 8, 9\n\t10]'
		)
		'multiline hanging indent in binary expr' |> t.eq(
			print('total := a + \nb+c*\nd')
			'total := a +\n\tb + c *\n\td'
		)
		'hanging indent after binary expr in delimited list' |> t.eq(
			print('1+[\n2\n]\na')
			'1 + [\n\t2\n]\na'
		)
		'delimited list in hanging indent line' |> t.eq(
			// NOTE: this isn't the desired behavior, but a limitation of the
			// current hanging indent algorithm. It's also not horrible, just
			// "meh". Since it's passable and fixing it will complicate the
			// algorithm significantly, I've chosen to live with it
			// until/unless I find a simpler fix.
			//
			// In essence, this amounts to a "documented bug".
			print('1+\n[\n\t2\n\t]\na')
			'1 +\n\t[\n\t2\n]\na'
		)

		// edge cases and regressions
		'correctly keeps shebang line' |> t.eq(
			print('#!/usr/bin/env oak --flag\n\n2 + 3')
			'#!/usr/bin/env oak --flag\n\n2 + 3'
		)
		'empty block body' |> t.eq(
			print('{{\n\t\t}}')
			'{ {\n} }'
		)
		'indentation collapsing' |> t.eq(
			print('([{\ntype: :key\nval:_\n}])')
			'([{\n\ttype: :key\n\tval: _\n}])'
		)
		'unary operators as fn body' |> t.eq(
			// NOTE: here, the unary - operator in front of n should ideally
			// not be spaced from n. This test doesn't test desired behavior,
			// but instead a documented bug that's a consequence of the high
			// level design of syntax.Printer.
			//
			// Because the token sequence [_, ')', '-'] can be either something
			// like a function definition (below) or a binary expression like
			// (1 + 2) - 3, from a token stream we cannot differentiate one
			// from another case without fully parsing the AST. To keep with
			// the token-based formatter design, we had to assume one, and we
			// currently assume the much more common latter case. I think this
			// is an acceptable tradeoff for a much simpler and faster printing
			// algorithm, but this may change in the future.
			print('fn() !t.passed?, fn neg(n) -n')
			'fn() !t.passed?, fn neg(n) - n'
		)
		'fn body with comments' |> t.eq(
			print('fn {\n\t3 + 4// what is going on?\t\n}')
			'fn {\n\t3 + 4 // what is going on?\n}'
		)
		'unary negation and atoms in if expr' |> t.eq(
			print('if !  try(){:fail->:error}')
			'if !try() { :fail -> :error }'
		)
		'complex expr in if condition' |> t.eq(
			print('if {\nwhatever_\n} { if[23]{30 ->false} -> c }\nif( 2 + 3 ) { }')
			'if {\n\twhatever_\n} { if [23] { 30 -> false } -> c }\nif (2 + 3) {}'
		)
		'just a list in function body' |> t.eq(
			print('fn name[1,2]\t ,fn[ line,col ]')
			'fn name [1, 2], fn [line, col]'
		)
		'common idiom of if directly inside with callback' |> t.eq(
			print('with fs.readFile( path , ) fn (file) if file {\n//body\n}')
			'with fs.readFile(path) fn(file) if file {\n\t//body\n}'
		)
	}
}

